/*
 * Copyright 2020 Supasin Tatiyanupanwong
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        // Huawei services configuration
        maven { url 'https://developer.huawei.com/repo/' }
    }

    dependencies {
        // Huawei services configuration
        classpath 'com.huawei.agconnect:agcp:1.3.0.300'
    }
}

apply plugin: 'com.android.application'

android {
    namespace 'dev.supasintatiyanupanwong.samples.android.kits.maps'

    compileSdkVersion 33

    defaultConfig {
        applicationId 'dev.supasintatiyanupanwong.samples.android.kits.maps'

        minSdkVersion 19
        targetSdkVersion 33
    }

    flavorDimensions 'vendor'
    productFlavors {
        google
        huawei {
            applicationIdSuffix '.huawei'
        }
        amazon {
            applicationIdSuffix '.amazon'

            minSdkVersion 22 // The minimum supported API level is Fire OS 5 (API 22).
        }
        tomtom {
            applicationIdSuffix '.tomtom'

            minSdkVersion 21 // The minimum supported API level for TomTom Maps SDK
        }
        herelite {
            applicationIdSuffix '.herelite'

            minSdkVersion 21 // The minimum supported API level for HERE SDK
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    googleImplementation project(':maps-google')
    huaweiImplementation project(':maps-huawei')
    amazonImplementation project(':maps-amazon')
    tomtomImplementation project(':maps-tomtom')
    hereliteImplementation project(':maps-here-lite')
    hereliteImplementation files('src/herelite/libs/heresdk-lite-android-4.12.8.0.10008.aar')

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
}

apply plugin: 'com.huawei.agconnect'
