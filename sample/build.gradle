/*
 * Copyright 2020 Supasin Tatiyanupanwong
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        // Huawei services configuration
        maven { url 'https://developer.huawei.com/repo/' }
    }

    dependencies {
        // Huawei services configuration
        classpath 'com.huawei.agconnect:agcp:1.3.0.300'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId 'me.tatiyanupanwong.supasin.android.samples.kits.maps'

        minSdkVersion 19
        targetSdkVersion 29
    }

    flavorDimensions 'vendor'
    productFlavors {
        google
        huawei {
            applicationIdSuffix '.huawei'
        }
    }
}

configurations {
    google
    huawei

    googleImplementation.extendsFrom(google)
    googleCompileOnly.extendsFrom(huawei)

    huaweiImplementation.extendsFrom(huawei)
    huaweiCompileOnly.extendsFrom(google)
}

dependencies {
    google project(':maps-google')
    huawei project(':maps-huawei')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
}

apply plugin: 'com.huawei.agconnect'
